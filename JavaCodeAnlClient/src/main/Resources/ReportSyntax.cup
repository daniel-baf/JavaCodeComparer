/**************************************************/
/************** SECTION 1: USER CODE **************/
/**************************************************/

package Backend.Objects.Parsers;

import Backend.Objects.Lexers.ReportLexer;
import java_cup.runtime.*;

/**************************************************/
/************ SECTION 2: USER METHODS *************/
/**************************************************/

parser code {:

    public ReportParser(ReportLexer lexer) {
        super(lexer);
    }

    // error methods
    public void report_error(String message, Object info) {
        System.out.println("public void report_error");
    }
    
    public void report_fatal_error(String message, Object info) {
        System.out.println("public void report_fatal_error");
    }

    public void syntax_error(Symbol cur_token) {
        System.out.println(String.format("El error es el simbolo: %1$s, en posicion: %2$d, %3$d valor: %4$s", repSym.terminalNames[cur_token.sym], cur_token.left, cur_token.right, cur_token.value));        
    }

    public void unrecovered_syntax_error(Symbol cur_token) {
        System.out.println("unrecovered sintax error");
    }

:}

/**************************************************/
/************ SECTION 3: LEXICAL RULES ************/
/**************************************************/

// from lexer


terminal INTEGER, STRING_TYPE;

terminal HTML_C, HTML_O, H1_C, H1_O, H2_C, H2_O, TABLE_C, TABLE_O, FOR_C, FOR_O, TR_C, TR_O, TD_C, TD_O, TH_C, TH_O,
  BR, UNKNOWN, ID, DIV, NUMBER, DOT, BRACK_C, BRACK_O, SEMICOLON, COLON, EQUALS, PLUS, LESS, MULT, PAR_C, PAR_O,  
  DOLAR, TYPE, SCORE, UNTIL, NAME, CLASSES, RESULT_T, METHODS, FUNCTION, ITERATOR, VARIABLES, PARAMETERS,
  COMMENTS, TEXT, GT, STRING;

non terminal main, var_declaration, typeOf, variables_section, arithm_expr, master_element, sym_arithm, element, id_vals, result_object, result_object_attrs,
    result_data_classes, result_data_variables, result_data_methods, result_data_comments, result_data_variables_children, result_data_methods_children, html_section,
    html_content, html_declarations, html_declarations_global, table_declaration, for_declaration, h1_declaration, h2_declaration, br_declaration, data_set,
    table_content, table_row_declaration, table_row_sub, table_row_content, table_row_headers, table_row_bodies, table_row_headers_content, table_row_bodies_content,
    for_open, brack_and_num, table_row_content_sub;

// precedence
precedence left PLUS, LESS;
precedence left DIV, MULT;
precedence left PAR_O, PAR_C;


start with main;

main::=
      variables_section html_section
    | html_section
;

// HTML
html_section::=
      HTML_O HTML_C
    | HTML_O html_content HTML_C
;

html_content::=
      html_declarations
    | html_content html_declarations
;

html_declarations::=
      html_declarations_global
    | table_declaration
    | for_declaration
    | error
;

html_declarations_global::=
      h1_declaration
    | h2_declaration
    | br_declaration
;

h1_declaration::=
      H1_O H1_C
    | H1_O data_set H1_C
;

h2_declaration::=
      H2_O H2_C
    | H2_O data_set H2_C
;

br_declaration::=BR;

table_declaration::=
      TABLE_O TABLE_C
    | TABLE_O table_content TABLE_C
;

table_content::=
      table_row_declaration
    | table_content table_row_declaration
;

table_row_declaration::= // TODO add for
        table_row_sub
      | for_open table_row_sub FOR_C
;

table_row_sub::=
      TR_O TR_C
    | TR_O table_row_content TR_C
;

table_row_content::=
      table_row_content_sub
    | for_open table_row_content_sub FOR_C
;

table_row_content_sub::=
      table_row_headers
    | table_row_bodies    
;

table_row_headers::=
      table_row_headers_content
    | table_row_headers table_row_headers_content
;

table_row_headers_content::=
      TH_O TH_C
    | TH_O data_set TH_C
;

table_row_bodies::=
      table_row_bodies_content
    | table_row_bodies table_row_bodies_content
;

table_row_bodies_content::=
      TD_O TD_C
    | TD_O data_set TD_C
;


for_declaration::=
      for_open FOR_C
    | for_open html_content FOR_C
;

for_open::=
    FOR_O ITERATOR COLON id_vals UNTIL COLON element SEMICOLON GT
;


// VARIABLES

variables_section::=
      var_declaration
    | variables_section var_declaration
;


var_declaration::=
      typeOf ID SEMICOLON
    | typeOf ID EQUALS arithm_expr SEMICOLON
    | error SEMICOLON
;

// VARIABLES DATA
arithm_expr::=
    master_element
  | arithm_expr sym_arithm arithm_expr
  | PAR_O arithm_expr PAR_C
;

data_set::=
      DOLAR PAR_O id_vals PAR_C DOLAR
    | element
;

// TERMINAL GENERATORS
master_element::=
      element
    | STRING
;

element::=
      NUMBER
    | id_vals
    | error
;

id_vals::=
      ID
    | result_object
;

result_object::=
      RESULT_T
    | RESULT_T DOT result_object_attrs
;

result_object_attrs::=
      SCORE
    | result_data_classes
    | result_data_variables
    | result_data_methods
    | result_data_comments
;

result_data_classes::=
      CLASSES
    | CLASSES brack_and_num
    | CLASSES brack_and_num DOT NAME
;

result_data_variables::=
      VARIABLES
    | VARIABLES brack_and_num
    | VARIABLES brack_and_num DOT result_data_variables_children
;

result_data_methods::=
      METHODS
    | METHODS brack_and_num
    | METHODS brack_and_num DOT result_data_methods_children
;

result_data_comments::=
      COMMENTS
    | COMMENTS brack_and_num
    | COMMENTS brack_and_num DOT TEXT
;

result_data_variables_children::=
      NAME
    | TYPE
    | FUNCTION
;

result_data_methods_children::=
      NAME
    | TYPE
    | PARAMETERS
;

brack_and_num::=
      BRACK_O NUMBER BRACK_C
    | BRACK_O ID BRACK_C
;

sym_arithm::=
      PLUS
    | LESS
    | MULT
    | DIV
;

typeOf::=
      INTEGER
    | STRING_TYPE
;